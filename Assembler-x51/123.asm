;SERIAL RECEIVE FROM PC STD KEYBOARD

;RUN SUCCESS

;2 LINE CONTINUOUS DISPLAY WITH BACKSPACE CORRECTION ON 16x2 LCD

;ASTER
;28/07/2008



;CONNECTIONS :::::::::::::::::::::: 		;LCD- NORMAL
						;RXD- P3.0
						;TXD- P3.1

$NOMOD51
$INCLUDE (89C52.MCU)




ORG 0000H
SJMP 0030H
		

ORG 0030H
	


START:		CLR A 
		MOV P1,A
		MOV P2,A
		MOV P3,A
		SETB P3.0
		MOV R1,A


INIT:		MOV A,#38H
		LCALL COMMAND
		MOV A,#0EH
		LCALL COMMAND
		MOV A,#06H
		LCALL COMMAND
		MOV A,#01H
		LCALL COMMAND



STARTS:		MOV SCON,#50H
		ANL PCON,#7FH
		
		MOV IE,#90H
		MOV IP,#10H
		
		MOV T2CON,#30H
		MOV RCAP2H,#0FFH	;9600 bps
		MOV RCAP2L,#0D9H
		
		SETB TR2
		
		
HERE:		SJMP HERE








COMMAND:	LCALL READY
		CLR P1.2
		CLR P1.1
		MOV P2,A
		SETB P1.3
		NOP
		CLR P1.3
		RET


READY: 		SETB P2.7
		CLR P1.1
		SETB P1.2
BUSY:		CLR P1.3
		SETB P1.3
		JB P2.7,BUSY
		NOP 
		CLR P1.3
		RET


DISPLAY:	LCALL READY
		CLR P1.2
		SETB P1.1
		MOV P2,A
		SETB P1.3
		NOP
		CLR P1.3
		RET

READ:		CLR P1.1
		SETB P1.2
		MOV P2,#0FFH
		CLR P1.3
		NOP
		SETB P1.3
		NOP
		MOV A,P2
		ANL A,#7FH
		CLR P1.3
		RET





ORG 0023H
LJMP 1000H
ORG 1000H

RXDC:		NOP
		JNB RI,EXIT
		CLR RI
		MOV A,SBUF
		MOV R5,A

BACKSPACE:	CJNE R5,#08H,NEXT
		LCALL READ
		CJNE A,#40H,NXTC
		MOV A,#10H
		SJMP DCRC
NXTC:		CJNE A,#00H,DCRC
		MOV A,#50H
DCRC:		DEC A
		ORL A,#80H
		MOV 33H,A
		LCALL COMMAND
		MOV A,#20H
		LCALL DISPLAY
		MOV A,33H
		LCALL COMMAND
		DEC R1
		MOV A,R1
		ANL A,#80H
		JZ SKIPL
		MOV R1,#00H
SKIPL:		SJMP EXIT
		
		

NEXT:		CJNE R1,#20H,CONT
		MOV R1,#00H
		MOV A,#80H
		LCALL COMMAND
CONT:		CJNE R1,#10H,CONT2
CONT2:		JNC LINE2 			;CARRY => R1< 21H	
		SJMP GOON
LINE2:		CJNE R1,#10H,GOON
		MOV A,#0C0H
		LCALL COMMAND				
		
GOON:		MOV A,R5	
		LCALL DISPLAY
		
		INC R1
		MOV A,R1
		ANL A,#80H
		JZ EXIT
		MOV R1,#00H
EXIT:		RETI




END

